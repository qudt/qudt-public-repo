SELECT
    ?this
    ?actualSymbol
    ?calculatedSymbol
    ?sourceRelation
    (qudt:symbol as ?path)

WHERE {
    # union of 2 very different subqueries
    # first: calculate symbols via factor units
    # second: calculate symbols via scalingof (see below)
    {
        BIND(qudt:hasFactorUnit as ?sourceRelation)
        ?this
            a qudt:Unit ;
            optional {
                ?this qudt:symbol ?actualSymbol
            }
        {
            SELECT
                ?this
                (SAMPLE(?denominatorLocalname) as ?denomName)
                (SAMPLE(?numeratorLocalname) as ?numerName)
                (IF(BOUND(?denomName),
                    IF(BOUND(?numerName),
                        CONCAT(?numerName,"-PER-",?denomName),
                        CONCAT("PER-",?denomName)
                    ),
                    ?numerName)
                AS ?unitLocalnameReconstructed)
                (SAMPLE(?denominatorSymbol) as ?denomSymbTmp)
                (IF(
                    CONTAINS(?denomSymbTmp, "·"),
                    CONCAT("(",?denomSymbTmp, ")"),
                    ?denomSymbTmp)
                AS ?denomSymb)
                (SAMPLE(?numeratorSymbol) as ?numerSymb)
                (IF(BOUND(?denomSymb),
                    IF(BOUND(?numerSymb),
                        CONCAT(?numerSymb,"/",?denomSymb),
                        CONCAT("/",?denomSymb)
                    ),
                    ?numerSymb)
                AS ?calculatedSymbol)
            WHERE
            {

                {
                    SELECT ?this
                        (GROUP_CONCAT(?baseSymbol; separator="·") as ?numeratorSymbol)
                        (GROUP_CONCAT(?baseLocalname; separator="-") as ?numeratorLocalname)
                    {
                        SELECT DISTINCT ?this ?base ?baseLocalname ?posInNumerator ?baseSymbol
                        {

                            ?this
                            a qudt:Unit ;
                            BIND(qfn:localname(?this) as ?unitLocalname )
                            BIND(
                                IF(
                                    CONTAINS(?unitLocalname, "PER"),
                                    STRBEFORE(?unitLocalname, "-PER-"),
                                    ?unitLocalname)
                                AS ?unitLocalnameNumerator)

                            ?this qudt:hasFactorUnit ?fu .
                            ?fu   qudt:exponent ?exp .
                            ?fu qudt:hasUnit ?base .
                            ?base qudt:symbol ?symbol .
                            BIND(qfn:localname(?base) AS ?baseLocalnameTmp)
                            BIND(
                                IF(
                                    ABS(?exp) = 1,
                                    ?baseLocalnameTmp,
                                    CONCAT(?baseLocalnameTmp, STR(abs(?exp))))
                                AS ?baseLocalname)
                            BIND(CONCAT("(?<=(-|^))",?baseLocalname,"(?=(-|$)).*$") as ?regexBaseLocalname)
                            BIND(STRLEN(REPLACE(?unitLocalnameNumerator, ?regexBaseLocalname, "")) AS ?posInNumeratorTmp)
                            BIND(IF(?posInNumeratorTmp = STRLEN(?unitLocalnameNumerator), -1, ?posInNumeratorTmp) AS ?posInNumerator)
                            BIND(CONCAT(?symbol, qfn:exponentSuperscript(?exp, true)) AS ?baseSymbol)
                            FILTER(?posInNumerator > -1)
                        } ORDER BY ?this ?posInNumerator
                    } GROUP BY ?this
                }
                UNION
                {
                    SELECT ?this
                        (GROUP_CONCAT(?baseSymbol; separator="·") as ?denominatorSymbol)
                        (GROUP_CONCAT(?baseLocalname; separator="-") as ?denominatorLocalname)
                    {
                        SELECT DISTINCT ?this ?base ?baseLocalname ?posInDenominator ?baseSymbol
                        {

                            ?this
                            a qudt:Unit ;
                            BIND(qfn:localname(?this) as ?unitLocalname )
                            BIND(STRAFTER(?unitLocalname, "PER-") AS ?unitLocalnameDenominator)

                            ?this qudt:hasFactorUnit ?fu .
                            ?fu   qudt:exponent ?exp .
                            ?fu qudt:hasUnit ?base .
                            ?base qudt:symbol ?symbol .
                            BIND(qfn:localname(?base) AS ?baseLocalnameTmp)
                            BIND(
                                IF(
                                    ABS(?exp) = 1,
                                    ?baseLocalnameTmp,
                                    CONCAT(?baseLocalnameTmp, STR(abs(?exp))))
                                AS ?baseLocalname)
                            BIND(CONCAT("(?<=(-|^))",?baseLocalname,"(?=(-|$)).*$") as ?regexBaseLocalname)
                            BIND(STRLEN(REPLACE(?unitLocalnameDenominator, ?regexBaseLocalname, "")) AS ?posInDenominatorTmp)
                            BIND(IF(?posInDenominatorTmp = STRLEN(?unitLocalnameDenominator), -1, ?posInDenominatorTmp) AS ?posInDenominator)
                            BIND(CONCAT(?symbol, qfn:exponentSuperscript(?exp, true)) AS ?baseSymbol)
                            FILTER(?posInDenominator > -1)
                        } ORDER BY ?this ?posInDenominator
                    } GROUP BY ?this
                }
            } GROUP BY ?this
            HAVING(qfn:localname(?this) = ?unitLocalnameReconstructed)
        }
    }


    UNION

    # second subquery: calculate symbols via scalingOf
    {
        {
            BIND(qudt:scalingOf as ?sourceRelation)
            ?this
                a qudt:Unit ;
                qudt:scalingOf ?base .
            OPTIONAL {
                ?this qudt:symbol ?actualSymbol
            }
            ?base qudt:symbol ?baseUnitSymbol .
            BIND(qfn:localname(?this) as ?unitLocalname )
            BIND(qfn:localname(?base) AS ?baseLocalname)
            OPTIONAL {
                ?this qudt:prefix ?prefix .
                ?prefix qudt:symbol ?prefixSymbol .
                BIND(qfn:localname(?prefix) AS ?prefixLocalname)
            }
            BIND(
                IF(BOUND(?prefixSymbol),
                    CONCAT(?prefixSymbol, ?baseUnitSymbol),
                    ?baseUnitSymbol)
                AS ?calculatedSymbol)
            BIND(
                IF(BOUND(?prefixLocalname),
                    CONCAT(?prefixLocalname, ?baseLocalname),
                    ?baseLocalname)
                AS ?scaledUnitLocalnameReconstructed)
            FILTER( ?unitLocalname = ?scaledUnitLocalnameReconstructed )
        }
    }
    FILTER(!BOUND(?actualSymbol) || ?calculatedSymbol != ?actualSymbol)
}

