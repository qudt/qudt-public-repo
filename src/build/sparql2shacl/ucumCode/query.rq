SELECT
    ?this
    ?actualUcumCode
    (STRDT(?calculatedUcumCodeString, qudt:UCUMcs) AS ?calculatedUcumCode)
    ?sourceRelation
    (qudt:ucumCode as ?path)

WHERE {
    # union of 2 very different subqueries
    # first: calculate ucumCodes via factor units
    # second: calculate ucumCodes via scalingof (see below)
    {
        BIND(qudt:hasFactorUnit as ?sourceRelation)
        ?this
            a qudt:Unit ;
            optional {
                ?this qudt:ucumCode ?actualUcumCode
            }
        {
            SELECT
                ?this
                (SAMPLE(?denominatorLocalname) as ?denomName)
                (SAMPLE(?numeratorLocalname) as ?numerName)
                (IF(BOUND(?denomName),
                    IF(BOUND(?numerName),
                        CONCAT(?numerName,"-PER-",?denomName),
                        CONCAT("PER-",?denomName)
                    ),
                    ?numerName)
                AS ?unitLocalnameReconstructed)
                (SAMPLE(?denominatorUcumCode) as ?denomSymb)
                (SAMPLE(?numeratorUcumCode) as ?numerSymb)
                (IF(BOUND(?denomSymb),
                    IF(BOUND(?numerSymb),
                        CONCAT(?numerSymb,".",?denomSymb),
                        ?denomSymb
                    ),
                    ?numerSymb)
                AS ?calculatedUcumCodeString)
            WHERE
            {

                {
                    SELECT ?this
                        (GROUP_CONCAT(?baseUcumCode; separator=".") as ?numeratorUcumCode)
                        (GROUP_CONCAT(?baseLocalname; separator="-") as ?numeratorLocalname)
                    {
                        SELECT DISTINCT ?this ?base ?baseLocalname ?posInNumerator ?baseUcumCode
                        {

                            ?this
                            a qudt:Unit ;
                            BIND(qfn:localname(?this) as ?unitLocalname )
                            BIND(
                                IF(
                                    CONTAINS(?unitLocalname, "PER"),
                                    STRBEFORE(?unitLocalname, "-PER-"),
                                    ?unitLocalname)
                                AS ?unitLocalnameNumerator)

                            ?this qudt:hasFactorUnit ?fu .
                            ?fu   qudt:exponent ?exp .
                            ?fu qudt:hasUnit ?base .
                            ?base qudt:ucumCode ?ucumCode .
                            BIND(qfn:localname(?base) AS ?baseLocalnameTmp)
                            BIND(
                                IF(
                                    ABS(?exp) = 1,
                                    ?baseLocalnameTmp,
                                    CONCAT(?baseLocalnameTmp, STR(abs(?exp))))
                                AS ?baseLocalname)
                            BIND(CONCAT("(?<=(-|^))",?baseLocalname,"(?=(-|$)).*$") as ?regexBaseLocalname)
                            BIND(STRLEN(REPLACE(?unitLocalnameNumerator, ?regexBaseLocalname, "")) AS ?posInNumeratorTmp)
                            BIND(IF(?posInNumeratorTmp = STRLEN(?unitLocalnameNumerator), -1, ?posInNumeratorTmp) AS ?posInNumerator)
                            BIND(CONCAT(STR(?ucumCode), IF(?exp != 1, STR(?exp), "")) AS ?baseUcumCode)
                            FILTER(?posInNumerator > -1)
                        } ORDER BY ?this ?posInNumerator
                    } GROUP BY ?this
                }
                UNION
                {
                    SELECT ?this
                        (GROUP_CONCAT(?baseUcumCode; separator=".") as ?denominatorUcumCode)
                        (GROUP_CONCAT(?baseLocalname; separator="-") as ?denominatorLocalname)
                    {
                        SELECT DISTINCT ?this ?base ?baseLocalname ?posInDenominator ?baseUcumCode
                        {

                            ?this
                            a qudt:Unit ;
                            BIND(qfn:localname(?this) as ?unitLocalname )
                            BIND(STRAFTER(?unitLocalname, "PER-") AS ?unitLocalnameDenominator)

                            ?this qudt:hasFactorUnit ?fu .
                            ?fu   qudt:exponent ?exp .
                            ?fu qudt:hasUnit ?base .
                            ?base qudt:ucumCode ?ucumCode .
                            BIND(qfn:localname(?base) AS ?baseLocalnameTmp)
                            BIND(
                                IF(
                                    ABS(?exp) = 1,
                                    ?baseLocalnameTmp,
                                    CONCAT(?baseLocalnameTmp, STR(abs(?exp))))
                                AS ?baseLocalname)
                            BIND(CONCAT("(?<=(-|^))",?baseLocalname,"(?=(-|$)).*$") as ?regexBaseLocalname)
                            BIND(STRLEN(REPLACE(?unitLocalnameDenominator, ?regexBaseLocalname, "")) AS ?posInDenominatorTmp)
                            BIND(IF(?posInDenominatorTmp = STRLEN(?unitLocalnameDenominator), -1, ?posInDenominatorTmp) AS ?posInDenominator)
                            BIND(CONCAT(STR(?ucumCode), IF(?exp != 1, STR(?exp), "")) AS ?baseUcumCode)
                            FILTER(?posInDenominator > -1)
                        } ORDER BY ?this ?posInDenominator
                    } GROUP BY ?this
                }
            } GROUP BY ?this
            HAVING(qfn:localname(?this) = ?unitLocalnameReconstructed)
        }
    }


    UNION

    # second subquery: calculate ucumCodes via scalingOf
    {
        {
            BIND(qudt:scalingOf as ?sourceRelation)
            ?this
                a qudt:Unit ;
                qudt:scalingOf ?base .
            OPTIONAL {
                ?this qudt:ucumCode ?actualUcumCode
            }
            ?base qudt:ucumCode ?baseUnitUcumCode .
            BIND(qfn:localname(?this) as ?unitLocalname )
            BIND(qfn:localname(?base) AS ?baseLocalname)
            OPTIONAL {
                ?this qudt:prefix ?prefix .
                ?prefix qudt:ucumCode ?prefixUcumCode .
                BIND(qfn:localname(?prefix) AS ?prefixLocalname)
            }
            BIND(
                IF(BOUND(?prefixUcumCode),
                    CONCAT(STR(?prefixUcumCode), STR(?baseUnitUcumCode)),
                    STR(?baseUnitUcumCode))
                AS ?calculatedUcumCodeString)
            BIND(
                IF(BOUND(?prefixLocalname),
                    CONCAT(?prefixLocalname, ?baseLocalname),
                    ?baseLocalname)
                AS ?scaledUnitLocalnameReconstructed)
            FILTER( ?unitLocalname = ?scaledUnitLocalnameReconstructed )
        }
    }
    FILTER(!BOUND(?actualUcumCode) || ?calculatedUcumCodeString != STR(?actualUcumCode))
}

