PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh:   <http://www.w3.org/ns/shacl#>
PREFIX qudt: <http://qudt.org/schema/qudt/>

INSERT {
  ?classUses qudt:usesProperty ?propertyUsed .
}
WHERE {
  {
    SELECT DISTINCT ?classUses ?propertyUsed
    WHERE {
      {
        # Pattern 1: NodeShape with sh:targetClass
        ?shape3 a sh:NodeShape ;
                sh:targetClass ?classUses ;
                sh:property ?ps3 .
      }
      UNION
      {
        # Pattern 2: class-as-shape
        ?classUses a rdfs:Class ;
                   sh:property ?ps3 .
      }
      UNION
      {
        # Pattern 3: NodeShape-only "classes"
        ?classUses a sh:NodeShape ;
                   sh:property ?ps3 .
        FILTER NOT EXISTS { ?classUses a rdfs:Class }
      }

      ?ps3 sh:path ?propertyUsed .

      FILTER isIRI(?classUses)
      FILTER isIRI(?propertyUsed)
    }
  }
}